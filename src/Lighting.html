<!--  Demo of diffuse lighting. -->		

<!DOCTYPE html>
<html lang="en">
<head>
<meta charset="utf-8" />
<title>Diffuse lighting</title>
</head>

<body> 
	<canvas id="theCanvas" width="600" height="400">
    Please use a browser that supports "canvas"
    </canvas>

<p>
Keyboard controls:
<ul style="list-style:none;">
<li>SPACE - pause rotation
<li>x - rotate about x axis
<li>y - rotate about y axis
<li>z - rotate about z axis
<li>o - Reset to initial state
<li>
</ul>

<script id="vertexLightingShader" type="x-shader/x-vertex">
uniform mat4 model;
uniform mat4 view;
uniform mat4 projection;
uniform mat3 normalMatrix;
uniform vec4 lightPosition;

attribute vec4 a_Position;
attribute vec3 a_Normal;

varying vec4 fColor;

void main() 
{
  // hard-coded surface color
  vec4 color = vec4(0.0, 1.0, 0.0, 1.0);

  // ambient term based on surface color
  vec4 ambient = 0.2 * color;

  // convert position to eye coords
  vec4 positionEye = view * model * a_Position;

  // convert light position to eye coords
  vec4 lightEye = view * lightPosition;

  // vector from vertex position toward light
  vec3 L = normalize((lightEye - positionEye).xyz);

  // transform normal vector into eye coords
  vec3 N = normalize(normalMatrix * a_Normal);

  // alternative, if there is no non-uniform scaling
  //vec3 N = (view * model * vec4(a_Normal, 0.0)).xyz;

  // Lambert's law, clamp negative values to zero
  float diffuseFactor = max(0.0, dot(L, N));

  // scale color by above factor
  fColor = color * diffuseFactor + ambient;

  // restore alpha to fully opaque
  fColor.a = 1.0;

  // don't forget to compute the vertex position!
  gl_Position = projection * view * model * a_Position;
}
</script>

<script id="fragmentLightingShader" type="x-shader/x-fragment">
precision mediump float;
varying vec4 fColor;
void main() 
{
  gl_FragColor = fColor;
}
</script>

<script id="vertexColorShader" type="x-shader/x-vertex">
uniform mat4 transform;
attribute vec4 a_Position;
attribute vec4 a_Color;
varying vec4 color;
void main() 
{
  color = a_Color;
  gl_Position = transform * a_Position;
}
</script>

<script id="fragmentColorShader" type="x-shader/x-fragment">
precision mediump float;
varying vec4 color;
void main() 
{
  gl_FragColor = color;
}
</script>

		<script src="http://www.cs.iastate.edu/~smkautz/cs336f14/examples/teal_book/webgl-utils.js"></script>
		<script src="http://www.cs.iastate.edu/~smkautz/cs336f14/examples/teal_book/webgl-debug.js"></script>
		<script src="http://www.cs.iastate.edu/~smkautz/cs336f14/examples/teal_book/cuon-utils.js"></script>
		<script src="http://www.cs.iastate.edu/~smkautz/cs336f14/examples/teal_book/cuon-matrix.js"></script>
		<script src="js/Lighting.js"></script>
		<script>window.onload = main</script>
</body>
</html>